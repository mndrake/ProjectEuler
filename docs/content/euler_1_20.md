
###Multiples of 3 and 5
####Problem 1
If we list all the natural numbers below 10 that are multiples of 3 or 5, we get
3, 5, 6 and 9. The sum of these multiples is 23.

Find the sum of all the multiples of 3 or 5 below 1000.


    [1..999] |> List.filter(fun x -> x%3=0||x%5=0) |> List.sum




    val it : int = 233168



###Even Fibonacci numbers
####Problem 2
Each new term in the Fibonacci sequence is generated by adding the previous two
terms. By starting with 1 and 2, the first 10 terms will be:

1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not exceed
four million, find the sum of the even-valued terms.


    let fibs = (1,2) |> Seq.unfold (fun (a,b) -> Some(a,(b,b+a)))
    fibs |> Seq.takeWhile (fun n -> n < 4000000)
         |> Seq.filter (fun n -> n%2=0)
         |> Seq.sum




    val it : int = 4613732



###Largest prime factor
####Problem 3
The prime factors of 13195 are 5, 7, 13 and 29.

What is the largest prime factor of the number 600851475143 ?


    // don't need to start at 2 since number is odd
    
    let factor number =
        let rec loopy n k factors = 
          if k*k > n then
              k::factors |> List.max
          elif n%k=0L then 
              loopy (n/k) k (k::factors)
          else
              loopy n (k+2L) factors
        loopy number 3L []
        
    factor 600851475143L




    val it : int64 = 1471L



###Largest palindrome product
####Problem 4
A palindromic number reads the same both ways. The largest palindrome made from
the product of two 2-digit numbers is 9009 = 91 × 99.

Find the largest palindrome made from the product of two 3-digit numbers.


    let isPalindrome n = 
        string n = System.String((string n).ToCharArray() |> Array.rev)
    
    // assuming the range of answers is 900 to 999
    
    seq [ for a in 900 .. 999 do
            for b in 900 .. a do
                if isPalindrome(a*b) then
                        yield a*b ]
    |> Seq.max




    val it : int = 906609



###Smallest multiple
####Problem 5
2520 is the smallest number that can be divided by each of the numbers from 1 to
10 without any remainder.

What is the smallest positive number that is evenly divisible by all of the
numbers from 1 to 20?


    /// greatest common divisor
    let rec gcd a b = if b = 0L then a else gcd b (a%b)
    /// least common multiple
    let lcm a b = a * b / gcd a b
    
    [1L .. 20L] |> List.reduce lcm




    val it : int64 = 232792560L



###Sum square difference
####Problem 6
The sum of the squares of the first ten natural numbers is,

$1^2 + 2^2 + ... + 10^2 = 385$
The square of the sum of the first ten natural numbers is,

$(1 + 2 + ... + 10)^2 = 55^2 = 3025$
Hence the difference between the sum of the squares of the first ten natural
numbers and the square of the sum is $3025 − 385 = 2640$.

Find the difference between the sum of the squares of the first one hundred
natural numbers and the square of the sum.


    let sqr x = x*x
    ([1..100] |> List.sum |> sqr) - ([1..100] |> List.sumBy sqr)




    val it : int = 25164150



###10001st prime
####Problem 7
By listing the first six prime numbers: 2, 3, 5, 7, 11, and 13, we can see that
the 6th prime is 13.

What is the 10 001st prime number?


    let isPrime n =
        let isqrt x = int(sqrt(float x))
        {2 .. isqrt n} |> Seq.exists (fun i -> n%i=0) |> not
    
    // assuming that the 10001st prime is less than 1M
    {2 .. 1000000}
    |> Seq.filter isPrime
    |> Seq.nth 10000  // F# indices start at 0




    val it : int = 104743



###Largest product in a series
####Problem 8
Find the greatest product of five consecutive digits in the 1000-digit number.


    open System.IO
    
    let file = __SOURCE_DIRECTORY__ + "/txt/p008.txt"
    
    File.ReadAllLines(file)
    |> Array.reduce (+)
    |> fun n -> n.ToCharArray()
    |> Seq.windowed 5
    |> Seq.map (fun s -> s |> Array.map(string >> int)
                           |> Array.reduce (*))
    |> Seq.max




    val it : int = 40824



###Special Pythagorean triplet
####Problem 9
A Pythagorean triplet is a set of three natural numbers, $a < b < c$, for which,

$a^2 + b^2 = c^2$
For example, $3^2 + 4^2 = 9 + 16 = 25 = 5^2$.

There exists exactly one Pythagorean triplet for which $a + b + c = 1000$.
Find the product $abc$.


    let isTriplet a b c = a*a+b*b=c*c
    seq [ for a = 1 to 1000 do
            for b = 1 to a do
              if isTriplet a b (1000-a-b) then
                yield a*b*(1000-a-b) ]
    |> Seq.head




    val it : int = 31875000



###Summation of primes
###Problem 10
The sum of the primes below 10 is 2 + 3 + 5 + 7 = 17.

Find the sum of all the primes below two million.


    // prime sieve from Jon Harrop
    // http://fsharpnews.blogspot.com/2010/02/sieve-of-eratosthenes.html
    let prime =
          let a = ResizeArray[2]
          let grow() =
            let p0 = a.[a.Count-1]+1
            let b = Array.create p0 true
            for di in a do
              let rec loop i =
                if i < b.Length then
                  b.[i] <- false
                  loop(i + di)
              let i0 = p0/di*di
              loop(if i0<p0 then i0+di-p0 else i0-p0)
            for i=0 to b.Length-1 do
              if b.[i] then a.Add(p0+i)
          fun n ->
            while n >= a.Count do
              grow()
            int64 a.[n]
    
    Seq.initInfinite prime
    |> Seq.takeWhile (fun n -> n<2000000L)
    |> Seq.sum




    val it : int64 = 142913828922L



###Largest product in a grid
####Problem 11

In the 20×20 grid below, four numbers along a diagonal line have been marked in
red.

08 02 22 97 38 15 00 40 00 75 04 05 07 78 52 12 50 77 91 08
49 49 99 40 17 81 18 57 60 87 17 40 98 43 69 48 04 56 62 00
81 49 31 73 55 79 14 29 93 71 40 67 53 88 30 03 49 13 36 65
52 70 95 23 04 60 11 42 69 24 68 56 01 32 56 71 37 02 36 91
22 31 16 71 51 67 63 89 41 92 36 54 22 40 40 28 66 33 13 80
24 47 32 60 99 03 45 02 44 75 33 53 78 36 84 20 35 17 12 50
32 98 81 28 64 23 67 10 <font color='red'>26</font> 38 40 67 59 54 70 66 18 38
64 70
67 26 20 68 02 62 12 20 95 <font color='red'>63</font> 94 39 63 08 40 91 66 49
94 21
24 55 58 05 66 73 99 26 97 17 <font color='red'>78</font> 78 96 83 14 88 34 89
63 72
21 36 23 09 75 00 76 44 20 45 35 <font color='red'>14</font> 00 61 33 97 34 31
33 95
78 17 53 28 22 75 31 67 15 94 03 80 04 62 16 14 09 53 56 92
16 39 05 42 96 35 31 47 55 58 88 24 00 17 54 24 36 29 85 57
86 56 00 48 35 71 89 07 05 44 44 37 44 60 21 58 51 54 17 58
19 80 81 68 05 94 47 69 28 73 92 13 86 52 17 77 04 89 55 40
04 52 08 83 97 35 99 16 07 97 57 32 16 26 26 79 33 27 98 66
88 36 68 87 57 62 20 72 03 46 33 67 46 55 12 32 63 93 53 69
04 42 16 73 38 25 39 11 24 94 72 18 08 46 29 32 40 62 76 36
20 69 36 41 72 30 23 88 34 62 99 69 82 67 59 85 74 04 36 16
20 73 35 29 78 31 90 01 74 31 49 71 48 86 81 16 23 57 05 54
01 70 54 71 83 51 54 69 16 92 33 48 61 43 52 01 89 19 67 48

The product of these numbers is $26 × 63 × 78 × 14 = 1788696$.

What is the greatest product of four adjacent numbers in the same direction (up,
down, left, right, or diagonally) in the 20×20 grid?


    open System.IO
    
    let file = __SOURCE_DIRECTORY__ + "/txt/p011.txt"
    
    let table = 
        File.ReadAllLines(file)
        |> Array.map (fun row -> 
            row.Split([|' '|])
            |> Array.map (string >> int))
        |> array2D
        
    let rows, columns = table |> Array2D.length1, table |> Array2D.length2
    
    let collect i j di dj =
        let rec loop i j step acc =
            if step=0 then acc
            elif i<0 || i>=rows || j<0 || j>=columns then 0
            else loop (i+di) (j+dj) (step-1) acc*table.[i,j]
        loop i j 4 1
        
    let goRight i j = collect i j 0 1
    let goDown i j = collect i j 1 0
    let goDiag1 i j = collect i j 1 1
    let goDiag2 i j = collect i j 1 -1
    
    seq { for i=1 to rows do 
            for j=1 to columns do 
              yield [goRight i j;goDown i j;goDiag1 i j;goDiag2 i j]
                    |> List.max  }
    |> Seq.max




    val it : int = 70600674



###Highly divisible triangular number
####Problem 12
The sequence of triangle numbers is generated by adding the natural numbers. So
the 7th triangle number would be $1 + 2 + 3 + 4 + 5 + 6 + 7 = 28$. The first ten
terms would be:

$1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...$

Let us list the factors of the first seven triangle numbers:

 1: 1
 3: 1,3
 6: 1,2,3,6
10: 1,2,5,10
15: 1,3,5,15
21: 1,3,7,21
28: 1,2,4,7,14,28

We can see that 28 is the first triangle number to have over five divisors.

What is the value of the first triangle number to have over five hundred
divisors?


    // using the prime generator from problem 10
    
    let factors (n:int64) =
        let rec loop k m factors =
            let p = prime k
            if p*p > m then m::factors
            elif m%p=0L then loop (k) (m / p) (p::factors)
            else loop (k+1) m factors
        loop 0 n []
        
    let divisorSigma n =
        n |> factors
          |> List.toSeq
          |> Seq.countBy id
          |> Seq.fold (fun acc (p,n) -> acc*(n+1)) 1
          
    Seq.unfold (fun (n,m) -> Some(m+n,(n+1L,m+n))) (1L,0L)
    |> Seq.find (fun n -> divisorSigma n > 500)




    val it : int64 = 76576500L



###Large sum
####Problem 13

Work out the first ten digits of the sum of the following one-hundred 50-digit
numbers.


    open System.IO
    
    let file = __SOURCE_DIRECTORY__ + "/txt/p013.txt"
    
    File.ReadAllLines(file)
    |> Array.sumBy (fun n -> bigint.Parse(n))
    |> fun n -> (string n).[0..9]




    val it : string = "5537376230"



###Longest Collatz sequence
####Problem 14

The following iterative sequence is defined for the set of positive integers:

n → n/2 (n is even)
n → 3n + 1 (n is odd)

Using the rule above and starting with 13, we generate the following sequence:

13 → 40 → 20 → 10 → 5 → 16 → 8 → 4 → 2 → 1
It can be seen that this sequence (starting at 13 and finishing at 1) contains
10 terms. Although it has not been proved yet (Collatz Problem), it is thought
that all starting numbers finish at 1.

Which starting number, under one million, produces the longest chain?

NOTE: Once the chain starts the terms are allowed to go above one million.


    // memoize pattern from Don Syme (creator of F#)
    // http://blogs.msdn.com/b/dsyme/archive/2007/05/31/a-sample-of-the-memoization-pattern-in-f.aspx?Redirected=true
    
    let memoize f =
        let cache = System.Collections.Generic.Dictionary<_, _>()
        fun x ->
            if cache.ContainsKey(x) then cache.[x]
            else let res = f x
                 cache.[x] <- res
                 res
    
    let chain =
        memoize(fun n ->
            let rec loop k acc =
                match k with
                |1L -> acc
                |k when k%2L=0L -> loop (k/2L) (acc+1)
                |k -> loop (3L*k+1L) (acc+1)
            loop n 1)
    
    [1L .. 1000000L]
    |> List.map (fun n -> n,chain n)
    |> List.maxBy snd
    |> fst




    val it : int64 = 837799L



###Lattice paths
####Problem 15

Starting in the top left corner of a 2×2 grid, and only being able to move to
the right and down, there are exactly 6 routes to the bottom right corner.

How many such routes are there through a 20×20 grid?


      let bigFact n = Seq.reduce (*) {1I..n}
      bigFact(40I)/bigFact(20I)/bigFact(20I)




    val it : Numerics.BigInteger = 137846528820



###Power digit sum
####Problem 16

$2^{15} = 32768$ and the sum of its digits is $3 + 2 + 7 + 6 + 8 = 26$.

What is the sum of the digits of the number $2^{1000}$?


    (string(pown 2I 1000)).ToCharArray()
    |> Array.sumBy(fun n -> int(string n))
    




    val it : int = 1366



###Number letter counts
####Problem 17

If the numbers 1 to 5 are written out in words: one, two, three, four, five,
then there are $3 + 3 + 5 + 4 + 4 = 19$ letters used in total.

If all the numbers from 1 to 1000 (one thousand) inclusive were written out in
words, how many letters would be used?

NOTE: Do not count spaces or hyphens. For example, 342 (three hundred and forty-
two) contains 23 letters and 115 (one hundred and fifteen) contains 20 letters.
The use of "and" when writing out numbers is in compliance with British usage.


    let readBasics x =
        match x with
        |0 -> 0
        |1|2|6|10 -> 3
        |4|5|9 -> 4
        |3|7|8|40|50|60 -> 5
        |11|12|20|30|80|90 -> 6
        |15|16|70 -> 7
        |13|14|18|19 -> 8
        |17 -> 9
        |_ -> failwithf "failed with %A" x
     
    let rec readLength x =
        if x = 1000 then
            3 + 8
        elif x <= 20 then
            readBasics x
        elif x < 100 then
            let ten = x/10*10
            let last = x%10
            readBasics ten + readBasics last
        else
            let hund = x/100
            if x%100 = 0 then
                readBasics hund + 7
            else
                readBasics hund + 7 + 3 + readLength (x%100) // 7 for 'hundred' 3 for 'and'
    {1..1000} |> Seq.map readLength |> Seq.sum




    val it : int = 21124



###Maximum path sum I
####Problem 18
By starting at the top of the triangle below and moving to adjacent numbers on
the row below, the maximum total from top to bottom is 23.

3
7 4
2 4 6
8 5 9 3

That is, $3 + 7 + 4 + 9 = 23$.

Find the maximum total from top to bottom of the triangle below:

75
95 64
17 47 82
18 35 87 10
20 04 82 47 65
19 01 23 75 03 34
88 02 77 73 07 63 67
99 65 04 28 06 16 70 92
41 41 26 56 83 40 80 70 33
41 48 72 33 47 32 37 16 94 29
53 71 44 65 25 43 91 52 97 51 14
70 11 33 28 77 73 17 78 39 68 17 57
91 71 52 38 17 14 91 43 58 50 27 29 48
63 66 04 68 89 53 67 30 73 16 69 87 40 31
04 62 98 27 23 09 70 98 73 93 38 53 60 04 23

NOTE: As there are only 16384 routes, it is possible to solve this problem by
trying every route. However, Problem 67, is the same challenge with a triangle
containing one-hundred rows; it cannot be solved by brute force, and requires a
clever method! ;o)


    open System.IO
    
    let file = __SOURCE_DIRECTORY__ + "/txt/p018.txt"
    
    let tbl = 
        File.ReadAllLines(file)
        |> Array.map (fun n -> 
            n.Split([|' '|])
            |> Array.map int)
       
    tbl
    |> Array.rev
    |> Array.reduce (fun a b -> 
        a |> Seq.windowed 2
          |> Seq.map (Seq.max)
          |> Seq.zip b
          |> Seq.map (fun x -> fst x + snd x)
          |> Seq.toArray)
    |> Seq.head




    val it : int = 1074



###Counting Sundays
####Problem 19

You are given the following information, but you may prefer to do some research
for yourself.

1 Jan 1900 was a Monday.
Thirty days has September,
April, June and November.
All the rest have thirty-one,
Saving February alone,
Which has twenty-eight, rain or shine.
And on leap years, twenty-nine.
A leap year occurs on any year evenly divisible by 4, but not on a century
unless it is divisible by 400.
How many Sundays fell on the first of the month during the twentieth century (1
Jan 1901 to 31 Dec 2000)?


    open System
    
    let begDt = DateTime.Parse("1 Jan 1901")
    let endDt = DateTime.Parse("31 Dec 2000")
    
    Seq.init ((endDt-begDt).Days+1) (fun x -> begDt.AddDays(float x))
    |> Seq.filter (fun x -> x.Day = 1 && x.DayOfWeek.Equals(DayOfWeek.Sunday))
    |> Seq.length




    val it : int = 171



###Factorial digit sum
####Problem 20
n! means n × (n − 1) × ... × 3 × 2 × 1

For example, 10! = 10 × 9 × ... × 3 × 2 × 1 = 3628800,
and the sum of the digits in the number 10! is 3 + 6 + 2 + 8 + 8 + 0 + 0 = 27.

Find the sum of the digits in the number 100!


    Seq.reduce (*) {1I..100I} 
    |> fun n -> (string n).ToCharArray()
    |> Array.map (string >> int)
    |> Array.sum




    val it : int = 648


